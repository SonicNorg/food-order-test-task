plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id "org.openapi.generator" version "5.0.0"
}

group 'name.nepavel.foodorder'
version = parent.version

repositories {
    mavenCentral()
}

dependencies {
    //зависимость на скомпиленные классы сгенерированного мавен-проекта
    compile files("$buildDir/generated/target/classes") {
        builtBy "compileMavenProject"
    }
    //зависимость на зависимости сгенерированного мавен-проекта
    compile files("$buildDir/generated/target/libs") {
        builtBy "downloadMavenDependencies"
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//собиралка мавеном
task compileMavenProject(type: Exec) {
    workingDir "$buildDir/generated/"
    commandLine "$projectDir/mvnw.cmd", "clean", "package"
}

//скачивалка зависимостей мавен-проекта
task downloadMavenDependencies(type: Exec) {
    workingDir "$buildDir/generated/"
    commandLine "$projectDir/mvnw.cmd", "dependency:copy-dependencies", "-DoutputDirectory=target/libs"
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/restaapi.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "name.nepavel.foodorder.restaapi.api"
//        invokerPackage = "org.openapi.example.invoker"
    modelPackage = "name.nepavel.foodorder.restaapi.model"
//        modelFilesConstrainedTo = [
//                "Error"
//        ]
    configOptions = [
            dateLibrary: "java8",
            library    : "spring-cloud"
    ]
}

compileJava.dependsOn tasks.openApiGenerate
compileMavenProject.dependsOn tasks.openApiGenerate
downloadMavenDependencies.dependsOn tasks.openApiGenerate

//билд - ничто без зависимостей
tasks.build.finalizedBy(tasks.shadowJar)